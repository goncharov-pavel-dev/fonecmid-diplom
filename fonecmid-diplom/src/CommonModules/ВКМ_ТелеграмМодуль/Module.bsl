#Область Методы

Процедура РассылкаУведомленийВТелеграм() Экспорт

	ВыборкаСообщенийЗапрос = Новый Запрос;
	ВыборкаСообщенийЗапрос.Текст = "ВЫБРАТЬ
	|	ВКМ_Уведомления.Текст,
	|	ВКМ_Уведомления.Ссылка
	|ИЗ
	|	Справочник.ВКМ_Уведомления КАК ВКМ_Уведомления";
	
	Выборка = ВыборкаСообщенийЗапрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОтправитьСообщениеВТелеграм(Выборка.Текст);
		СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СправочникОбъект.Удалить();
	КонецЦикла;

КонецПроцедуры

Процедура ОтправитьСообщениеВТелеграм(Текст)
	
	ИдентификаторЧата = Константы.ВКМ_ИдентификаторЧата.Получить();
	Токен = Константы.ВКМ_ТокенТелеграмБота.Получить();
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Content-Type", "application/json");
	
	Соединение = Новый HTTPСоединение("api.telegram.org",,,,,,Новый ЗащищенноеСоединениеOpenSSL());
	Запрос = Новый HTTPЗапрос("/bot" + Токен + "/sendMessage", Заголовки);
	
	ТелоСообщения = Новый Структура();
	ТелоСообщения.Вставить("chat_id", ИдентификаторЧата);
	ТелоСообщения.Вставить("text", Текст);
	
	Запрос.УстановитьТелоИзСтроки(ЗаписатьЗначениеJSON(ТелоСообщения));
	
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение Ответ.ПолучитьТелоКакСтроку();
	КонецЕсли;

КонецПроцедуры

Процедура ЗапланироватьОтправкуСообщений(ВходящийТекстСообщения) Экспорт

	СправочникОбъект = Справочники.ВКМ_Уведомления.СоздатьЭлемент();
	СправочникОбъект.Текст = ВходящийТекстСообщения;
	СправочникОбъект.Записать();
	
КонецПроцедуры



#КонецОбласти